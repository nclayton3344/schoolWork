      ******************************************************************
      * A SIMPLE COBOL PROGRAM TO READ JCL IN-STREAM DATA
      * FROM A05IN DATASET
      * WRITE OUTPUT TO JCL OUTSTREAM
      * USING THE DISPLAY COMMAND
      * SOURCE CODE: CS332205.COBOL(ASSIGN05)
      * JCL: C332205.JCL(ASSIGN05)
      ******************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. ASSIGN05.
       AUTHOR. NATHAN CLAYTON.
       INSTALLATION. WTAMU.
       DATE-WRITTEN. 10/28/2018.
       DATE-COMPILED.
       SECURITY. CONFIDENTIAL.
      *
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER. BUFFALO-THUNDER.
       OBJECT-COMPUTER. BUFFALO-THUNDER.
       INPUT-OUTPUT SECTION.
      *FILE-CONTROL DEFINES OUR INPUT/OUTPUT FILES IN THE PROGRAM
       FILE-CONTROL.
           SELECT INFLATION-MASTER ASSIGN TO AS05IN
               ORGANIZATION IS SEQUENTIAL.
           SELECT INFLATION-REPORT ASSIGN TO AS05OUT
               ORGANIZATION IS SEQUENTIAL.
      *
       DATA DIVISION.
      *FILE SECTION AND FD DEFINES THE RECORDS FOR OUR INPUT/OUTPUT
       FILE SECTION.
       FD INFLATION-MASTER
           RECORDING MODE IS F
           RECORD CONTAINS 80 CHARACTERS.
       01 INFLATION-MASTER-IN.
           05 ITEM-NO-IN       PIC X(5).
           05 ITEM-DESC-IN     PIC X(20).
           05 ITEM-COST-IN          PIC 999V99.
       FD INFLATION-REPORT
           RECORDING MODE IS F
           RECORD CONTAINS 80 CHARACTERS.
       01 INFLATION-REPORT-OUT     PIC X(80).
      *WORKING STORAGE DEFINES DATA NEEDED FOR THE PROGRAM
       WORKING-STORAGE SECTION.

       01 ARE-THERE-MORE-RECORDS PIC X(3) VALUE "YES".
       01 LINE-COUNT          PIC 99 VALUE 0.
       01 YEAR-COUNTER        PIC 99.
       01 PAGE-COUNT          PIC 999.
       01 TEMP-DATA           USAGE COMP-2.

       01 WS-DATE.
           05 WS-YR         PIC 9999.
           05 WS-MONTH      PIC 99.
           05 WS-DAY        PIC 99.

       01 HEADER-LINE1.
           05            PIC X(4) VALUE SPACES.
           05 TITLE-HEAD PIC X(43)
                VALUE "I N F L A T I O N   R E P O R T".
           05 DATE-OUT.
                10 HEAD-MONTH PIC XX.
                10 FILLER     PIC X VALUE "/".
                10 HEAD-DAY   PIC XX.
                10 FILLER     PIC X VALUE "/".
                10 HEAD-YEAR  PIC XXXX.
           05 FILLER          PIC X(14) VALUE SPACES.
           05 PAGE-HEAD       PIC X(5) VALUE "PAGE ".
           05 PAGE-NUM        PIC ZZ9.

       01 ITEM-NUM.
           05                  PIC X(4) VALUE SPACES.
           05 ITEM-NO-DESC     PIC X(13) VALUE "ITEM NUMBER: ".
           05 ITEM-NO-OUT      PIC X(5).

       01 ITEM-DESC.
           05                  PIC X(4) VALUE SPACES.
           05 ITEM-DESC-DESC   PIC X(19) VALUE "ITEM DESCRIPTION:  ".
           05 ITEM-DESC-OUT    PIC X(20).

       01 ITEM-COST.
           05                  PIC X(4) VALUE SPACES.
           05 ITEM-COST-DESC   PIC X(13) VALUE "ITEM COST: ".
           05 ITEM-COST-OUT    PIC $$$$.99.

       01 ITEM-INF-HEADER.
           05 FILLER           PIC X(27) VALUE SPACES.
           05 YEAR-HEAD        PIC X(9) VALUE "YEAR".
           05 RATE-HEAD        PIC X(19) VALUE "INFLATION RATE".
           05 INF-COST-HEAD    PIC X(24)
                VALUE "ITEM COST WITH INFLATION".

       01 ITEM-INFLATION-OUT.
           05 FILLER           PIC X(28) VALUE SPACES.
           05 YEAR-OUT         PIC Z9.
           05 FILLER           PIC X(12) VALUE SPACES.
           05 INFLATION-RATE   PIC 99.
           05 PERC             PIC X VALUE "%".
           05 FILLER           PIC X(21) VALUE SPACES.
           05 INF-COST         PIC ZZ9.99.
           05 FILLER           PIC X(8).
      *
       PROCEDURE DIVISION.
       100-MAIN-MODULE.
           PERFORM 500-GET-DATE-DATA
           OPEN INPUT INFLATION-MASTER
           OPEN OUTPUT INFLATION-REPORT
           MOVE 1 TO PAGE-COUNT
           PERFORM 300-PRINT-HEADER
           PERFORM UNTIL ARE-THERE-MORE-RECORDS = "NO "
                IF LINE-COUNT IS GREATER THAN 28
                    PERFORM 300-PRINT-HEADER
                    MOVE 0 TO LINE-COUNT
                END-IF
                READ INFLATION-MASTER
                  AT END
                      MOVE 'NO ' TO ARE-THERE-MORE-RECORDS
                  NOT AT END
                        PERFORM 200-PRINT-ITEM-HEAD
                        PERFORM 400-CALCULATE-INFLATION
                END-READ
           END-PERFORM
           CLOSE INFLATION-MASTER
           CLOSE INFLATION-REPORT
           STOP RUN.

      *PRINT ITEM DESCRIPTION HEADING TO OUTPUT RECORD
       200-PRINT-ITEM-HEAD.
           MOVE ITEM-NO-IN TO ITEM-NO-OUT
           MOVE ITEM-NUM TO INFLATION-REPORT-OUT
           WRITE INFLATION-REPORT-OUT
           MOVE ITEM-DESC-IN TO ITEM-DESC-OUT
           MOVE ITEM-DESC TO INFLATION-REPORT-OUT
           WRITE INFLATION-REPORT-OUT
           MOVE ITEM-COST-IN TO ITEM-COST-OUT
           MOVE ITEM-COST TO INFLATION-REPORT-OUT
           WRITE INFLATION-REPORT-OUT
           MOVE ITEM-INF-HEADER TO INFLATION-REPORT-OUT
           WRITE INFLATION-REPORT-OUT
           ADD 4 TO LINE-COUNT.
      *PRINT THE HEADER FOR OUTPUT
       300-PRINT-HEADER.
           MOVE PAGE-COUNT TO PAGE-NUM OF HEADER-LINE1
           MOVE HEADER-LINE1 TO INFLATION-REPORT-OUT
           WRITE INFLATION-REPORT-OUT
           ADD 1 TO PAGE-COUNT
           ADD 1 TO LINE-COUNT.
      *CALCULATE TEN YEARS WORTH OF INFLATION AT 8% FOR FIRST 5 YEARS
      *6% FOR THE FOLLOWING YEARS AND OUTPUTS ITEM-INFLATION-OUT
       400-CALCULATE-INFLATION.
           COMPUTE TEMP-DATA = ITEM-COST-IN
           MOVE 1 TO YEAR-COUNTER
           MOVE 08 TO INFLATION-RATE
           PERFORM UNTIL YEAR-COUNTER IS GREATER THAN 5
                COMPUTE TEMP-DATA = TEMP-DATA * 1.08
                COMPUTE INF-COST ROUNDED = TEMP-DATA
                MOVE YEAR-COUNTER TO YEAR-OUT
                MOVE ITEM-INFLATION-OUT TO INFLATION-REPORT-OUT
                WRITE INFLATION-REPORT-OUT
                ADD 1 TO YEAR-COUNTER
                ADD 1 TO LINE-COUNT
           END-PERFORM
           MOVE 06 TO INFLATION-RATE
           PERFORM UNTIL YEAR-COUNTER IS GREATER THAN 10
                COMPUTE TEMP-DATA = TEMP-DATA * 1.06
                COMPUTE INF-COST ROUNDED = TEMP-DATA
                MOVE YEAR-COUNTER TO YEAR-OUT
                MOVE ITEM-INFLATION-OUT TO INFLATION-REPORT-OUT
                WRITE INFLATION-REPORT-OUT
                ADD 1 TO YEAR-COUNTER
                ADD 1 TO LINE-COUNT
           END-PERFORM.
      *GET CURRENT DATE AND STORE TO OUTPUT RECORD
       500-GET-DATE-DATA.
           MOVE FUNCTION CURRENT-DATE TO WS-DATE
           MOVE WS-YR OF WS-DATE TO HEAD-YEAR OF DATE-OUT
           OF HEADER-LINE1
           MOVE WS-MONTH OF WS-DATE TO HEAD-MONTH OF DATE-OUT
           OF HEADER-LINE1
           MOVE WS-DAY TO HEAD-DAY OF DATE-OUT
           OF HEADER-LINE1.
